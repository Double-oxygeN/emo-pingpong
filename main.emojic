👵
  Copyright 2017 Double_oxygeN

  Licensed under the Apache License, Version 2.0 (the "License");
  you may not use this file except in compliance with the License.
  You may obtain a copy of the License at

        http://www.apache.org/licenses/LICENSE-2.0

  Unless required by applicable law or agreed to in writing, software
  distributed under the License is distributed on an "AS IS" BASIS,
  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
  See the License for the specific language governing permissions and
  limitations under the License.
👵

📦 allegro 🔴

🕊 ↗️ 🍇
  🍰 x 🚀
  🍰 y 🚀

  🔓🐈 🆕 初期x 🚀 初期y 🚀 🍇
    🍮 x 初期x
    🍮 y 初期y
  🍉

  🔓🐖 ↔️ ➡️ 🚀 🍇
    🍎 x
  🍉

  🔓🐖 ↕️ ➡️ 🚀 🍇
    🍎 y
  🍉

  🔓🐖 ♻️ 新規x 🚀 新規y 🚀 🍇
    🍮➖ x x
    🍮➖ y y
    🍮➕ x 新規x
    🍮➕ y 新規y
  🍉

  🔓🐖 ➕ dx 🚀 dy 🚀 🍇
    🍮➕ x dx
    🍮➕ y dy
  🍉

  🔓🐖 ✖️ rx 🚀 ry 🚀 🍇
    🍮✖️ x rx
    🍮✖️ y ry
  🍉

  🔓🐖 👉 差分 ↗️ 🍇
    🍮➕ x ↔️ 差分
    🍮➕ y ↕️ 差分
  🍉

  🔓🐖 ↩️ 角度 🚀 速さの増分 🚀 🍇
    🍦 コサイン 📕 角度
    🍦 サイン 📓 角度
    🍦 xの異符号 ✖️ -1.0 ➗ x 🏧 x
    🍦 大きさ ➕⛷➕ ✖️ x x ✖️ y y 速さの増分

    ♻️🐕 ✖️✖️ 大きさ コサイン xの異符号 ✖️ 大きさ サイン
  🍉
🍉

🐇 🎾 🍇
  🍰 中心位置 ↗️
  🍰 速度 ↗️
  🍰 半径 🚀
  🍰 色 🎨

  🔓🐈 🏭 初期x 🚀 初期y 🚀 🍇
    🍮 中心位置 🔷↗️🆕 初期x 初期y
    🍮 速度 🔷↗️🆕 -5.0 0.0
    🍮 半径 8
    🍮 色 🔷🎨🆕 255 255 255 255
  🍉

  🔓🐖 ♻️ 初期x 🚀 初期y 🚀 🍇
    ♻️ 中心位置 初期x 初期y
    ♻️ 速度 -5.0 0.0
  🍉

  🔓🐖 ↔️ ➡️ 🚀 🍇
    🍎 ↔️ 中心位置
  🍉

  🔓🐖 ↕️ ➡️ 🚀 🍇
    🍎 ↕️ 中心位置
  🍉

  🔓🐖 🔄 ➡️ 🚀 🍇
    🍎 半径
  🍉

  🔓🐖 🚶 🍇
    👉 中心位置 速度
  🍉

  🔓🐖 ⏪ 度合い 🚀 🍇
    👴✖️ 速度 -1 1
    🍦 角度 ✖️ 🍩⚾️🚀 ➗ 度合い 9.0
    ↩️ 速度 角度 0.1
  🍉

  🔓🐖 ⏬ 🍇
    ✖️ 速度 1 -1
  🍉

  🔓🐖 🎨 アプリ 🏔 🍇
    🏀 アプリ ↔️ 中心位置 ↕️ 中心位置 半径 色
  🍉
🍉

🐇 🛡 🍇
  🍰 中心位置 ↗️
  🍰 速さ 🚀
  🍰 半幅 🚀
  🍰 半高さ 🚀
  🍰 色 🎨

  🔓🐈 🏭 初期x 🚀 初期y 🚀 🍇
    🍮 中心位置 🔷↗️🆕 初期x 初期y
    🍮 速さ 3
    🍮 半幅 3
    🍮 半高さ 24
    🍮 色 🔷🎨🆕 255 255 255 255
  🍉

  🔓🐖 ↔️ ➡️ 🚀 🍇
  🍎 ↔️ 中心位置
  🍉

  🔓🐖 ↕️ ➡️ 🚀 🍇
    🍎 ↕️ 中心位置
  🍉

  🔓🐖 💥 球 🎾 ➡️ 🍬🚀 🍇
    🍦 球のx位置 ↔️ 球
    🍦 球のy位置 ↕️ 球
    🍦 球の半径 🔄 球
    🍊 🎊 ◀️ ➖ ↔️ 中心位置 半幅 ➕ 球のx位置 球の半径
       🎊 ▶️ ➕ ↔️ 中心位置 半幅 ➖ 球のx位置 球の半径
       🎊 ◀️ ➖ ↕️ 中心位置 半高さ 球のy位置
          ▶️ ➕ ↕️ 中心位置 半高さ 球のy位置 🍇
      🍎 ➗ ➖ 球のy位置 ↕️ 中心位置 半高さ
    🍉
    🍎 ⚡
  🍉

  🔓🐖 🚶 上入力 👌 下入力 👌 🍇
    🍊 上入力 🍇
      ➕ 中心位置 0.0 ✖️ -1.0 速さ
    🍉
    🍊 下入力 🍇
      ➕ 中心位置 0.0 速さ
    🍉
  🍉

  🔓🐖 🎨 アプリ 🏔 🍇
    🗄 アプリ ➖ ↔️ 中心位置 半幅 ➖ ↕️ 中心位置 半高さ ➕ ↔️ 中心位置 半幅 ➕ ↕️ 中心位置 半高さ 色
  🍉
🍉

🏁 🍇
  🍩🙋🏔 🍇 アプリケーション 🏔
    🍦 画面幅 600
    🍦 画面高さ 600
    🍦 画面 🔷📺🆕 画面幅 画面高さ
    🏷 画面 🔤Ping Pong Game🔤

    🍦 背景色 🔷🎨🆕 0 0 0 255
    🍦 文字色 🔷🎨🆕 102 204 255 255
    🍦 フォント 🚇🔷🕉📄 🔤fonts/Megaphone Monospaced.ttf🔤 64

    🍦 タイマー 🔷⏲🆕 ➗ 1.0 60

    🍦 イヴェントキュー 🔷🗃🆕
    ⌨️ イヴェントキュー
    ⏲ イヴェントキュー タイマー

    🍰 動作中？ 👌
    🍮 動作中？ 👍

    🍰 上キー入力？ 👌
    🍰 下キー入力？ 👌
    🍮 上キー入力？ 👎
    🍮 下キー入力？ 👎

    🍮 カウンタ 0

    🍦 球 🔷🎾🏭 400 100
    🍦 自陣の棒 🔷🛡🏭 50 100
    🍦 敵陣の棒 🔷🛡🏭 500 300
    🍮 自点 0
    🍮 敵点 0

    🏁 タイマー
    🔁 動作中？ 🍇
      👴 ---入力部---
      🍦 イヴェント ⏳ イヴェントキュー

      🍊🍦 キーボード押す 🔲 イヴェント 📥 🍇
        🍦 キーコード 📟キーボード押す
        🍊 😛 キーコード 59 👵ESCキー👵 🍇
          🍮 動作中？ 👎
        🍉
        🍋 😛 キーコード 84 👵⬆️キー👵 🍇
          😀 🔤⬆️🔤
          🍮 上キー入力？ 👍
        🍉
        🍋 😛 キーコード 85 👵⬇️キー👵 🍇
          😀 🔤⬇️🔤
          🍮 下キー入力？ 👍
        🍉
      🍉
      🍊🍦 キーボード離す 🔲 イヴェント 📤 🍇
        🍦 キーコード 📟キーボード離す
        🍊 😛 キーコード 84 👵⬆️キー👵 🍇
          🍮 上キー入力？ 👎
        🍉
        🍋 😛 キーコード 85 👵⬇️キー👵 🍇
          🍮 下キー入力？ 👎
        🍉
      🍉

      👴 ---更新部---
      🍮➕ カウンタ 1
      🚶 球
      🚶 自陣の棒 上キー入力？ 下キー入力？
      🚶 敵陣の棒 ◀️ ↕️球 ↕️敵陣の棒 ▶️ ↕️球 ↕️敵陣の棒

      🍊🍦 度合い 💥 自陣の棒 球 🍇
        😀 🔡 度合い 4
        ⏪ 球 度合い
        😀 🍪 🔤(🔤 🔡 ↔️ 球 4 🔤, 🔤 🔡 ↕️ 球 4 🔤)🔤 🍪
      🍉
      🍊🍦 度合い 💥 敵陣の棒 球 🍇
        😀 🔡 度合い 4
        ⏪ 球 度合い
      🍉
      🍊 🎉 ◀️ ↕️球 0.0 ▶️ ↕️球 🚀画面高さ 🍇
        ⏬ 球
      🍉
      🍊 ◀️ ↔️ 球 0.0 🍇
        🍮➕ 敵点 1
        ♻️ 球 400 ↕️ 自陣の棒
      🍉
      🍋 ▶️ ↔️ 球 🚀画面幅 🍇
        🍮➕ 自点 1
        ♻️ 球 400 ↕️ 自陣の棒
      🍉

      👴 ---描画部---
      🚿 アプリケーション 背景色

      🔡 アプリケーション フォント 🔡自点 10 ➗ 🚀画面幅 3 ➗ 🚀画面高さ 2 🔷⚖↔️ 文字色
      🔡 アプリケーション フォント 🔡敵点 10 ➗ 🚀画面幅 1.5 ➗ 🚀画面高さ 2 🔷⚖↔️ 文字色
      🎨 球 アプリケーション
      🎨 自陣の棒 アプリケーション
      🎨 敵陣の棒 アプリケーション

      🎦 アプリケーション
    🍉
  🍉
🍉
